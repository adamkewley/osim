cmake_minimum_required(VERSION 3.15)

project(osim VERSION 0.0.1 LANGUAGES C CXX)

set(OSCDEPS_BUILD_ALWAYS OFF CACHE BOOL "set BUILD_ALWAYS on all dependency targets, useful when editing dependencies")

include(ExternalProject)  # ExternalProject_Add
include(GNUInstallDirs)  # CMAKE_INSTALL_LIBDIR

# setup `OSCDEPS_DEPENDENCY_CMAKE_ARGS`
#
# these are cache args that should be forwarded to external dependency projects
if(TRUE)
    set(OSCDEPS_FORWARDED_VARS

        CMAKE_CXX_COMPILER
        CMAKE_C_COMPILER
        CMAKE_CXX_FLAGS
        CMAKE_CXX_FLAGS_DEBUG
        CMAKE_CXX_FLAGS_MINSIZEREL
        CMAKE_CXX_FLAGS_RELEASE
        CMAKE_CXX_FLAGS_RELWITHDEBINFO
        CMAKE_C_FLAGS
        CMAKE_C_FLAGS_DEBUG
        CMAKE_C_FLAGS_MINSIZEREL
        CMAKE_C_FLAGS_RELEASE
        CMAKE_C_FLAGS_RELWITHDEBINFO
        CMAKE_BUILD_TYPE
        CMAKE_INSTALL_PREFIX
    )
    foreach(OSCDEPS_FORWARDED_VAR IN LISTS OSCDEPS_FORWARDED_VARS)
        list(APPEND OSCDEPS_DEPENDENCY_CMAKE_ARGS -D${OSCDEPS_FORWARDED_VAR}:STRING=${${OSCDEPS_FORWARDED_VAR}})
    endforeach()
    unset(OSCDEPS_FORWARDED_VARS)
endif()

ExternalProject_Add(OpenBLAS
    SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/third_party/OpenBLAS
    BUILD_ALWAYS ${OSCDEPS_BUILD_ALWAYS}
    CMAKE_CACHE_ARGS
        ${OSCDEPS_DEPENDENCY_CMAKE_ARGS}
	    -DC_LAPACK:BOOL=ON
	    -DBUILD_STATIC_LIBS:BOOL=ON
)

ExternalProject_Add(simbody
    DEPENDS OpenBLAS
    SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/third_party/simbody
    BUILD_ALWAYS ${OSCDEPS_BUILD_ALWAYS}
    CMAKE_CACHE_ARGS
        ${OSCDEPS_DEPENDENCY_CMAKE_ARGS}
        -DBUILD_USING_OTHER_LAPACK:STRING=${CMAKE_INSTALL_PREFIX}/lib/openblas.lib
        -DBUILD_VISUALIZER:BOOL=OFF
        -DBUILD_DYNAMIC_LIBRARIES:BOOL=OFF
        -DBUILD_STATIC_LIBRARIES:BOOL=ON
        -DBUILD_EXAMPLES:BOOL=OFF
        -DBUILD_TESTING:BOOL=OFF
        -DINSTALL_DOCS:BOOL=OFF
)
